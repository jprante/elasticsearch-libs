
configurations {
    source
}

sourceSets {
    generated {
        java {
            srcDir "$buildDir/generated-src"
        }
        output.dir("$buildDir/generated-src", builtBy: 'sourceDownload')
    }
    main {
        java {
            srcDirs += sourceSets.generated.java.srcDirs
        }
    }
}

dependencies {

    source "org.apache.lucene:lucene-analyzers-common:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-backward-codecs:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-core:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-grouping:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-highlighter:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-join:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-memory:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-misc:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-queries:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-queryparser:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-sandbox:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-spatial:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-spatial-extras:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-spatial3d:${property('lucene.version')}:sources"
    source "org.apache.lucene:lucene-suggest:${property('lucene.version')}:sources"

    compile project(':spatial4j')
    compile project(':s2geo')

}

task unpackSource(type: Copy) {
    configurations.source.each {
        if (it.toString().endsWith('sources.jar')) {
            from zipTree(it)
            into "$buildDir/generated-src/java"
            // our versions have public modifiers for modularized  lucene-test-framework
            exclude '**/Automaton.java'
            exclude '**/BaseDirectory.java'
            exclude '**/FieldQuery.java'
            exclude '**/ConcurrentMergeScheduler.java'
            exclude '**/Directory.java'
            exclude '**/DocumentsWriter.java'
            exclude '**/DocumentsWriterPerThreadPool.java'
            exclude '**/FieldInfo.java'
            exclude '**/FieldInfos.java'
            exclude '**/FilterLeafReader.java'
            exclude '**/IndexFileDeleter.java'
            exclude '**/IndexReader.java'
            exclude '**/IndexWriter.java'
            exclude '**/MergeReaderWrapper.java'
            exclude '**/Operations.java'
            exclude '**/SegmentReader.java'
            exclude '**/SegmentCoreReaders.java'
            exclude '**/RAMDirectory.java'
            exclude '**/RAMFile.java'
            exclude '**/RamUsageEstimator.java'
            exclude '**/search/Scorer.java'
        }
    }
}

task unpackResources(type: Copy) {
    dependsOn unpackSource
    from "$buildDir/generated-src/java"
    into "$buildDir/resources/main"
    include '**/*.txt'
    // for loading XML for controlling Lucene compound hyphenation
    include '**/hyphenation.dtd'
    // snowball stemming
    include '**/stem_ISO_8859_1.sbl'
    // for portuguese stemming
    include '**/portuguese.rslp'
    // omit jj, jflex
}

afterEvaluate {
    compileJava {
        dependsOn unpackSource, unpackResources
        inputs.property("moduleName", 'org.xbib.elasticsearch.lucene')
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath
            ]
            classpath = files()
        }
    }
    javadoc {
        inputs.property("moduleName", 'org.xbib.elasticsearch.lucene')
        doFirst {
            options.addStringOption('-module-path', classpath.asPath)
            classpath = files()
        }
    }
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

artifacts {
    archives sourcesJar, javadocJar
}
